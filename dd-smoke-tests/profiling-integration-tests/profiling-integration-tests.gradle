plugins {
  id "com.github.johnrengelman.shadow" version "4.0.4"
}

ext {
  minJavaVersionForTests = JavaVersion.VERSION_11
  // Zulu has backported profiling support
  forceJdk = ['ZULU8', 'ZULU11', '12']
  jmcVersion = '8.0.0-SNAPSHOT'
}

apply from: "${rootDir}/gradle/java.gradle"

description = 'Profiling Integration Tests.'

repositories {
  jcenter()
  maven {
    url "https://adoptopenjdk.jfrog.io/adoptopenjdk/jmc-libs-snapshots"
  }
}


jar {
  manifest {
    attributes(
      'Main-Class': 'datadog.smoketest.profiling.ProfilingTestApplication'
    )
  }
}

task fetchJmc(type: Exec) {
  group 'Build Setup'
  description 'Initializes and updates jmc git submodule'
  commandLine 'git', 'submodule', 'update', '--init', "${rootDir}/vendor/jmc"
}

dependencies {
  compile project(':dd-trace-api')

  testCompile project(':dd-smoke-tests')
  testCompile project(':dd-java-agent:agent-profiling:profiling-testing')
  testCompile "org.openjdk.jmc:common:$jmcVersion"
  testCompile "org.openjdk.jmc:flightrecorder:$jmcVersion"
  testCompile "org.openjdk.jmc:flightrecorder.rules:$jmcVersion"
  testCompile "org.openjdk.jmc:flightrecorder.rules.jdk:$jmcVersion"
}

tasks.withType(Test).configureEach {
  dependsOn shadowJar

  jvmArgs "-Ddatadog.smoketest.profiling.shadowJar.path=${tasks.shadowJar.archivePath}"
}
