plugins {
  id "com.github.johnrengelman.shadow"
}

// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "${rootDir}/gradle/java.gradle"

dependencies {
  compile deps.slf4j
  compile project(':dd-trace-api')

  compile project(':dd-java-agent:agent-profiling:profiling-uploader')
  compile project(':dd-java-agent:agent-profiling:profiling-controller')
  compile project(':dd-java-agent:agent-profiling:profiling-controller-openjdk')
}

// FIXME: ideally we would like to be able to compile this for java7, so we could run things without odd strange errors
// from `TracingAgent`.
// But currently switching this to Java7 leads to an awkward API error when java sees constructor with `Duration`
// arguments and freaks out even though it doesn't use it.
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

[JavaCompile, GroovyCompile].each {
  tasks.withType(it) {
    doFirst {
      // Disable '-processing' because some annotations are not claimed.
      // Disable '-options' because we are compiling for java8 without specifying bootstrap - intentionally.
      // Disable '-path' because we do not have some of the paths seem to be missing.
      options.compilerArgs.addAll(['-Xlint:all,-processing,-options,-path', '-Werror'])
      options.fork = true
      options.forkOptions.javaHome = file(System.env.JAVA_11_HOME)
    }
  }
}

configurations {
  // exclude bootstrap dependencies from shadowJar
  runtime.exclude module: deps.opentracing
  runtime.exclude module: deps.slf4j
  runtime.exclude group: 'org.slf4j'
  runtime.exclude group: 'io.opentracing'
}

shadowJar {
  dependencies {
    exclude(project(':dd-java-agent:agent-bootstrap'))
    exclude(project(':dd-trace-api'))
  }
}

jar {
  classifier = 'unbundled'
}
