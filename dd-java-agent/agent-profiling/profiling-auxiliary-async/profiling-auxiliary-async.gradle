plugins {
  id "com.github.johnrengelman.shadow"
  id "de.undercouch.download" version "5.0.1"
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'idea'
apply plugin: 'de.undercouch.download'

import de.undercouch.gradle.tasks.download.Download

def osName = System.getProperty('os.name', '').toLowerCase()
// currently, only linux binaries are included
minimumInstructionCoverage = osName.contains("linux") ? 0.5 : 0
minimumBranchCoverage = osName.contains("linux") ? 0.4 : 0

excludedClassesCoverage += [
  // enums with no additional functionality
  'com.datadog.profiling.controller.async.Arch',
  'com.datadog.profiling.controller.async.OperatingSystem',
  // --
  // although it is quite well covered jacoco complains about branch coverage due to exception handlers
  'com.datadog.profiling.auxiliary.async.AsyncProfilerRecording',
  'com.datadog.profiling.auxiliary.async.AsyncProfilerRecordingData',
  // the config event is a simple data holder
  'com.datadog.profiling.auxiliary.async.AsyncProfilerConfigEvent'
]

def apDependency = deps.asyncprofiler
def apFileName = "async-profiler-DD.jar"
def apDownloadDir = "$rootDir/build/agent-snapshot"
def apDownloadPath = "$apDownloadDir/$apFileName"
def apUrl = System.env.get('AP_TOOLS_URL')
// AP_TOOLS_URL environment variable has always precedence over the configured dependency
if (apUrl == null && (apDependency instanceof java.net.URL)) {
  // if the dependency is defined as URL make sure it is downloaded
  apUrl = apDependency
}
if (apUrl != null) {
  task downloadApTools(type: Download) {
    mkdir(apDownloadDir)
    src(apUrl)
    dest(apDownloadPath)
    overwrite(true)
    onlyIfModified(true)
  }

  compileJava.dependsOn downloadApTools
  apDependency = files(apDownloadPath)
}


dependencies {
  api project(':dd-java-agent:agent-profiling:profiling-controller')
  api project(':dd-java-agent:agent-profiling:profiling-auxiliary')
  implementation apDependency

  annotationProcessor deps.autoserviceProcessor
  compileOnly deps.autoserviceAnnotation

  implementation deps.slf4j

  testImplementation deps.jmc
  testImplementation deps.junit5
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

shadowJar {
  classifier ""
  include {
    def rslt = false
    rslt |= it.path == "com" || it.path == "com/datadog" || it.path.startsWith("com/datadog/")
    rslt |= it.path == "one" || it.path == "one/profiler" || it.path.startsWith("one/profiler/")
    rslt |= it.path == "META-INF" || it.path == "META-INF/services" || it.path.startsWith("META-INF/services/")
    // TODO: modify the filter to include other OS/arch combinations once the overhead is evaluated
    rslt |= it.path == "native-libs" || it.path.startsWith("native-libs/linux-x64") || it.path.startsWith("native-libs/linux-musl-x64")
    rslt |= (it.path.contains("ap-tools") && it.path.endsWith(".jar"))
    return rslt
  }
}

build.dependsOn shadowJar

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

[JavaCompile, GroovyCompile].each {
  tasks.withType(it).configureEach {
    doFirst {
      if (!System.env.JAVA_11_HOME) {
        throw new GradleException('JAVA_11_HOME must be set to build profiling auxiliary')
      }
      // Disable '-processing' because some annotations are not claimed.
      // Disable '-options' because we are compiling for java8 without specifying bootstrap - intentionally.
      // Disable '-path' because we do not have some of the paths seem to be missing.
      options.compilerArgs.addAll(['-Xlint:all,-processing,-options,-path'/*, '-Werror'*/])
      options.fork = true
      options.forkOptions.javaHome = file(System.env.JAVA_11_HOME)
    }
  }
}

idea {
  module {
    jdkName = '11'
  }
}
