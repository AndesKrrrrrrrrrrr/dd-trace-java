// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = 'org.http4s'
    module = 'http4s-blaze-server_2.13'
    versions = '[0.22.0,0.23.0)'
    assertInverse = true
  }
  fail {
    group = 'org.http4s'
    module = 'http4s-blaze-server_2.12'
    versions = '[0.22.0,0.23.0)'
  }
}

// We need to set up the spotless targets here before we apply the standard settings to avoid
// having all the scala files in the common test directories being added as well, making spotless
// fail because the files are outside this project directory
apply plugin: 'com.diffplug.spotless'
spotless {
  groovy {
    target('src/**/*.groovy')
  }
  scala {
    target('src/**/*.scala')
  }
}
project.ext.groovySkipJavaExclude = true

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'

apply plugin: 'org.unbroken-dome.test-sets'
testSets {
  latestDepTest {
    dirName = 'test'
  }
}

sourceSets {
  test.groovy.srcDir project(':dd-java-agent:instrumentation:http4s-blaze-server-0.22').sourceSets.test.groovy
  latestDepTest.groovy.srcDir project(':dd-java-agent:instrumentation:http4s-blaze-server-0.22').sourceSets.test.groovy

  test.scala.srcDir project(':dd-java-agent:instrumentation:http4s-blaze-server-0.22').sourceSets.test.scala
  latestDepTest.scala.srcDir project(':dd-java-agent:instrumentation:http4s-blaze-server-0.22').sourceSets.test.scala
}

tasks.named('compileScala').configure {
  // Scala only needs the declared dependencies
  classpath = sourceSets.main.compileClasspath
}

tasks.named('compileJava').configure {
  // Java depends on Scala
  classpath += files(sourceSets.main.scala.classesDirectory)
}

tasks.named('compileTestGroovy').configure {
  // Groovy tests depend on Scala tests
  classpath += files(sourceSets.test.scala.classesDirectory)
}

tasks.named('compileLatestDepTestGroovy').configure {
  // Groovy tests depend on Scala tests
  classpath += files(sourceSets.latestDepTest.scala.classesDirectory)
}

dependencies {
  main_java8CompileOnly deps.scala213
  main_java8CompileOnly group: 'org.http4s', name: 'http4s-blaze-server_2.13', version: '0.22.0'

  testImplementation group: 'org.http4s', name: 'http4s-blaze-server_2.13', version: '0.22.0'
  testImplementation group: 'org.http4s', name: "http4s-dsl_2.13", version: '0.22.0'
  testImplementation project(':dd-java-agent:instrumentation:scala-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.13')

  latestDepTestImplementation group: 'org.http4s', name: 'http4s-blaze-server_2.13', version: '0.22.+'
  latestDepTestImplementation group: 'org.http4s', name: "http4s-dsl_2.13", version: '0.22.+'
}
