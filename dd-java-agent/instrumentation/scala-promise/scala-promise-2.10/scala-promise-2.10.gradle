ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = 'org.scala-lang'
    module = "scala-library"
    versions = "[2.9.3,2.13)"
    assertInverse = true
  }
}

// We need to set up the spotless targets here before we apply the standard settings to avoid
// having all the scala files in the common test directories being added as well, making spotless
// fail because the files are outside this project directory
apply plugin: 'com.diffplug.spotless'
spotless {
  groovy {
    target('src/**/*.groovy')
  }
  scala {
    target('src/**/*.scala')
  }
}
project.ext.groovySkipJavaExclude = true

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/scala.gradle"

apply plugin: 'org.unbroken-dome.test-sets'
testSets {
  latestDepTest {
    dirName = 'test'
  }

  latestDepForkedTest {
    extendsFrom latestDepTest
    dirName = 'forkedTest'
  }
}

sourceSets {
  test.groovy.srcDir project(':dd-java-agent:instrumentation:scala-promise').sourceSets.test.groovy
  test.groovy.srcDir sourceSets.latestDepForkedTest.groovy
  latestDepTest.groovy.srcDir project(':dd-java-agent:instrumentation:scala-promise').sourceSets.test.groovy
  latestDepForkedTest.groovy.srcDir project(':dd-java-agent:instrumentation:scala-promise').sourceSets.test.groovy

  test.scala.srcDir project(':dd-java-agent:instrumentation:scala-promise').sourceSets.test.scala
  latestDepTest.scala.srcDir project(':dd-java-agent:instrumentation:scala-promise').sourceSets.test.scala
  latestDepForkedTest.scala.srcDir project(':dd-java-agent:instrumentation:scala-promise').sourceSets.test.scala
}

tasks.named("compileTestGroovy").configure {
  classpath += files(sourceSets.test.scala.classesDirectory)
  dependsOn "compileTestScala"
}

tasks.named("compileLatestDepTestGroovy").configure {
  classpath += files(sourceSets.latestDepTest.scala.classesDirectory)
  dependsOn "compileLatestDepTestScala"
}

tasks.named("compileLatestDepForkedTestGroovy").configure {
  classpath += files(sourceSets.latestDepForkedTest.scala.classesDirectory)
  dependsOn "compileLatestDepForkedTestScala"
}

tasks.named("latestDepTest").configure {
  dependsOn "latestDepForkedTest"

}

dependencies {
  compileOnly deps.scala210
  implementation project(':dd-java-agent:instrumentation:scala-promise')

  testImplementation deps.scala210
  testImplementation project(':dd-java-agent:instrumentation:scala-promise')

  latestDepTestImplementation deps.scala212
  latestDepTestImplementation project(':dd-java-agent:instrumentation:scala-promise')
}
