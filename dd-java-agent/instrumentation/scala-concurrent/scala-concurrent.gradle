ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = 'org.scala-lang'
    module = "scala-library"
    versions = "[2.8,2.12)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/scala.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

/**
 * This project has instrumentation to support Scala's copy of ForkJoinPool which was added in 2.8 and removed in 2.11.
 * It also has additional generic tests for scala context propagation support beyond 2.11 which are covered in latest12Test and latestDepTest.
 */
testSets {
  latestDepTest
  latest12Test {
    dirName = 'latestDepTest'
  }
  latest11Test {
    dirName = 'test'
  }
}
tasks.named("compileTestGroovy").configure {
  classpath += files(sourceSets.test.scala.classesDirectory)
}
tasks.named("compileLatest12TestGroovy").configure {
  classpath += files(sourceSets.latest12Test.scala.classesDirectory)
}
tasks.named("compileLatest11TestGroovy").configure {
  classpath += files(sourceSets.latest11Test.scala.classesDirectory)
}
tasks.named("compileLatestDepTestGroovy").configure {
  classpath += files(sourceSets.latestDepTest.scala.classesDirectory)
}

dependencies {
  // first version that works with the tests and can build on JDK11 (without dependency problems):
  compileOnly deps.scala210

  testImplementation deps.scala210
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')

  latest11TestImplementation deps.scala211
  latest12TestImplementation deps.scala212
  latestDepTestImplementation deps.scala213
  latestDepTestImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.13')
}

tasks.named("latestDepTest").configure {
  dependsOn "latest11Test"
  dependsOn "latest12Test"
}
